ext {
    githubProjectName = 'bayesian'
    dataDir = "$rootDir/data"

    versions = [
        gae: '1.9.28',
        jersey: '1.17',
        javasimon: '3.3.0',
        slf4j: '1.7.1',
        restassured: '1.8.1'
    ]

    libraries = [
        jersey : ["com.sun.jersey:jersey-servlet:$versions.jersey",
                "com.sun.jersey:jersey-core:$versions.jersey",
                "com.sun.jersey:jersey-server:$versions.jersey",
                "com.sun.jersey:jersey-json:$versions.jersey",
                "com.sun.jersey:jersey-grizzly2:$versions.jersey",
                "com.sun.jersey:jersey-grizzly2:$versions.jersey"],

        restassured : ["com.jayway.restassured:rest-assured:$versions.restassured",
                "com.jayway.restassured:json-path:$versions.restassured",
                'org.codehaus.groovy:groovy-all:2.1.3']
    ]
}

buildscript {
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

apply from: file('gradle/data-prep.gradle')
apply from: file('gradle/convention.gradle')
apply from: file('gradle/check.gradle')

subprojects {
    group = "org.lenition.$githubProjectName"
}

task gae(dependsOn: ':server:gaeRun')
task functionalTest(dependsOn: ':server:gaeFunctionalTest')

project(':server') {
    apply plugin: 'gae'
    sourceCompatibility = JavaVersion.VERSION_1_7

    dependencies {
        // see compatibile versions at https://code.google.com/p/googleappengine/wiki/WillItPlayInJava#Jersey
        gaeSdk "com.google.appengine:appengine-java-sdk:$versions.gae"

        compile libraries.jersey
        compile 'com.google.code.gson:gson:2.2.4'
        compile 'org.apache.commons:commons-lang3:3.1'
        compile 'commons-io:commons-io:2.4'

        providedCompile 'javax.servlet:servlet-api:2.5'

        testCompile 'junit:junit:4.11'
        testCompile libraries.restassured
        testCompile "com.google.appengine:appengine-testing:$versions.gae"
    }

    gae {
        optimizeWar = true     // see https://github.com/musketyr/gradle-fatjar-plugin/
        downloadSdk = true
        disableUpdateCheck = true
        appcfg {
            email = 'seth.rylan@gmail.com'
            passIn = true

            logs {
                severity = 1
                outputFile = file('gae.log')
            }

            app {
                id = 'bayesian-calibration'
            }
        }
    }
}

project(':persistence') {
    apply plugin: 'gae'
    sourceCompatibility = JavaVersion.VERSION_1_7

    dependencies {
        gaeSdk "com.google.appengine:appengine-java-sdk:$versions.gae"

        compile "com.google.appengine:appengine-api-1.0-sdk:$versions.gae"
        compile libraries.jersey
        compile 'com.google.code.gson:gson:2.2.4'
        compile 'org.apache.commons:commons-lang3:3.1'
        compile 'commons-io:commons-io:2.4'

        providedCompile 'javax.servlet:servlet-api:2.5'

        testCompile 'junit:junit:4.11'
        testCompile libraries.restassured
        testCompile "com.google.appengine:appengine-testing:$versions.gae"
        testCompile "com.google.appengine:appengine-api-labs:$versions.gae"
        testCompile "com.google.appengine:appengine-api-stubs:$versions.gae"
    }

    gae {
        optimizeWar = true     // see https://github.com/musketyr/gradle-fatjar-plugin/
        downloadSdk = true
        disableUpdateCheck = true
        appcfg {
            email = 'seth.rylan@gmail.com'
            passIn = true

            logs {
                severity = 1
                outputFile = file('gae.log')
            }

            app {
                id = 'bayesian-calibration'
            }
        }
    }
}
